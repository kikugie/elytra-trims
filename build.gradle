plugins {
	id 'fabric-loom' version '1.2-SNAPSHOT'
	id 'io.github.juuxel.loom-quiltflower' version '1.8.0'
	id "com.modrinth.minotaur" version "2.+"
	id 'maven-publish'
}

loom {
	accessWidenerPath = accessWidenerPath = file("src/main/resources/elytratrims.accesswidener")
}

version = project.mod_version + "+" + project.version_support
group = project.maven_group

repositories {
	maven { url 'https://jitpack.io' }
	maven { url "https://maven.terraformersmc.com/releases/" }
	maven { url 'https://maven.isxander.dev/releases' }
	maven { url "https://maven.shedaniel.me/" }
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	include api(annotationProcessor("com.github.LlamaLad7.mixinextras:mixinextras-fabric:${project.mixin_extras_version}"))

	modApi "com.terraformersmc:modmenu:${project.modmenu_version}"
	modImplementation "dev.isxander.yacl:yet-another-config-lib-fabric:${project.yacl_version}"
	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_version}") {
		exclude(group: "net.fabricmc.fabric-api")
	}
}

base {
	archivesName = project.archives_base_name
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17
}

jar {
	from("LICENSE") {
		rename { "${it}_${base.archivesName.get()}"}
	}
}

modrinth {
	token = System.getenv("MODRINTH_TOKEN")
	projectId = "elytra-trims"
	versionNumber = project.mod_version
	versionType = "release"
	uploadFile = remapJar
	gameVersions = ["1.19.4"] // Must be an array, even with only one version
	loaders = ["fabric", "quilt"]
	dependencies {
		required.project "fabric-api"
		optional.project "cloth-config"
		optional.project "yacl"
	}
	syncBodyFrom = rootProject.file("README.md").text
	changelog = rootProject.file("CHANGELOG.md").text
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
