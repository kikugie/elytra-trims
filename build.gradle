plugins {
    id 'maven-publish'
    id 'fabric-loom' version '1.2-SNAPSHOT' apply false

    // https://github.com/Juuxel/LoomQuiltflower
    id 'io.github.juuxel.loom-quiltflower' version '1.8.0' apply false

    // https://github.com/ReplayMod/preprocessor
    // https://github.com/Fallen-Breath/preprocessor
    id 'com.replaymod.preprocess' version 'e1050f4d72'

    // https://github.com/Fallen-Breath/yamlang
    id 'me.fallenbreath.yamlang' version '1.2.1' apply false

    id "com.modrinth.minotaur" version "2.+" apply false
    id "co.uzzu.dotenv.gradle" version "2.0.0"
}

preprocess {
    def mc119 = createNode('1.19.4', 1_19_04, 'yarn')
    def mcSnapshot = createNode('snapshot', 1_20_00, 'yarn')

    mc119.link(mcSnapshot, null)
}

tasks.register('buildAndGather') {
    subprojects {
        dependsOn project.name + ':build'
    }
    doFirst {
        println 'Gathering builds'
        copy {
            subprojects {
                def libDir = project.projectDir.toPath().resolve('build/libs')
                from(libDir) {
                    include '*.jar'
                    exclude '*-dev.jar', '*-sources.jar'
                }
                into 'build/libs/'
                duplicatesStrategy DuplicatesStrategy.INCLUDE
            }
        }
    }
}

task cleanPreprocessSources {
    doFirst {
        subprojects {
            def path = project.projectDir.toPath().resolve('build/preprocessed')
            path.toFile().deleteDir()

            def resPath = project.projectDir.toPath().resolve('build/resources')
            resPath.toFile().deleteDir()
        }
    }
}
